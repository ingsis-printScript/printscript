plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'jacoco'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'io.gitlab.arturbosch.detekt'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
kotlin {
    jvmToolchain(17)
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

jacoco {
    toolVersion = "0.8.10"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.00
            }
        }
    }
}

detekt {
    config = files("$rootDir/detekt.yml")
    buildUponDefaultConfig = false
}

check {
    dependsOn jacocoTestCoverageVerification
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}